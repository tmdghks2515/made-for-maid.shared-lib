plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id "maven-publish"
	id "com.google.protobuf" version "0.9.4"
	id 'org.jetbrains.kotlin.jvm' version '1.9.23'
}
ext {
	springGrpcVersion = "0.7.0"
}

group = 'io.madeformaid'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter"
	implementation 'org.springframework.boot:spring-boot-configuration-processor:3.4.4'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:3.4.4'
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	implementation 'com.auth0:java-jwt:4.4.0'
	implementation "io.grpc:grpc-protobuf:1.62.2"
	implementation "com.google.protobuf:protobuf-kotlin:3.25.2"
	implementation "io.grpc:grpc-kotlin-stub:1.4.1"
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.25.2"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.62.2"
		}
		grpckt {
			artifact = "io.grpc:protoc-gen-grpc-kotlin:1.4.1:jdk8@jar" // gRPC Kotlin plugin
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins {
				java {}
				kotlin {} // kotlin 코드도 함께 생성
			}
			task.plugins {
				grpc {}
				grpckt {}
			}
		}
	}
}

sourceSets {
	main {
		proto {
			srcDir "src/main/proto"
		}
	}
}

tasks.withType(Copy).configureEach {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/tmdghks2515/made-for-maid.shared-lib")
			credentials {
				username = project.findProperty("gpr.user") ?: ""
				password = project.findProperty("gpr.key") ?: ""
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}
dependencyManagement {
	imports {
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:$springGrpcVersion"
	}
}
